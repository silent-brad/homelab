{
  description = "NixOS Homelab with Skarabox + Self Host Blocks";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    selfhostblocks.url = "github:ibizaman/selfhostblocks";
    skarabox.url = "github:ibizaman/skarabox";

    nixos-generators.url = "github:nix-community/nixos-generators";
    nixos-generators.inputs.nixpkgs.follows = "nixpkgs";

    nixos-anywhere.url = "github:nix-community/nixos-anywhere";
    nixos-anywhere.inputs.nixpkgs.follows = "nixpkgs";

    nixos-facter-modules.url = "github:numtide/nixos-facter-modules";
    flake-parts.url = "github:hercules-ci/flake-parts";
    deploy-rs.url = "github:serokell/deploy-rs";
    colmena.url = "github:zhaofengli/colmena";
    sops-nix.url = "github:Mic92/sops-nix";
  };

  outputs = inputs@{ self, nixpkgs, flake-parts, ... }: flake-parts.lib.mkFlake { inherit inputs; } ({ config, ... }: {
    systems = [
      "x86_64-linux"
      "aarch64-linux"
      "x86_64-darwin"
      "aarch64-darwin"
    ];

    imports = [
      inputs.skarabox.flakeModules.default
      inputs.skarabox.flakeModules.deploy-rs
      inputs.skarabox.flakeModules.colmena
    ];

    skarabox.hosts = {
      homelab = let
        system = "x86_64-linux";
      in {
        # Use SelfHostBlocks' patched nixpkgs
        nixpkgs = inputs.selfhostblocks.lib.${system}.patchedNixpkgs;
        inherit system;
        
        # Generated by `nix run .`
        hostKeyPub = ./homelab/host_key.pub;
        
        # Your server's static IP
        ip = "192.168.12.137";
        sshPort = 2222;
        sshBootPort = 2223;
        
        # Generated by `nix run .#homelab-generateKnownHosts`
        knownHosts = ./homelab/known_hosts;

        modules = [
          inputs.selfhostblocks.nixosModules.default
          inputs.sops-nix.nixosModules.default
          self.nixosModules.homelab
        ];

        extraBeaconModules = [
          {
            environment.systemPackages = with nixpkgs.legacyPackages.${system}; [
              tmux
              htop
              vim
            ];
          }
        ];
      };
    };

    flake = {
      nixosModules = {
        homelab = {
          imports = [
            ./homelab/configuration.nix
          ];
        };
      };
    };
  });
}
